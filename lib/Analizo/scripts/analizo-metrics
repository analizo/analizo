#!/usr/bin/perl

=head1 NAME

analizo-metrics - analizo's metric reporting tool

=head1 SYNOPSIS

  analizo metrics [options] <input> [<input> ...]

=head1 DESCRIPTION

analizo metrics analyzes source code in the directories passed as arguments and
procudes a matrics report. Such report is written to the standard output in the
YAML format (see I<http://www.yaml.org/>)

analizo metrics is part of the analizo suite.

=head1 OPTIONS

=over

=item --extractor <extractor>

Define wich extractor method use to analise source code. Default is Doxyparse.

When using the Doxyparse extractor (default), all files matching the languages
supported by doxyparse are processed, unless I<--language> is used.

=item --help

Displays the manual page.

=item --list | -l

Displays metric list

=item <input>...

Tells analizo which source code directory(ies) you want to parse.

=item --output <file> | -o <file>

Writes the output to <file> instead of standard output.

=item --global-only | -g

Don't output the details about modules: only output global (project-wide) metrics.

=item --language <lang>

Process only filenames matching known extensions for the <I<lang>> programming
language. To see which languages are supported, pass B<--language list>.

=item --exclude <dirs> | -x <dirs>

Exclude <I<dirs>> (a colon-separated list of directories) from the analysis.
This is useful, for example, when you want to focus on production code and
exclude test code from the analysis. You could do that by passing something
like pass B<--exclude test>.

=back

=head1 OUTPUT FORMAT

The output is a stream of YAML documents. The first one presents metrics for
the project as a whole. The subsequent ones present per-module metrics, and thus
there will be as many of them as there are modules in your project.

=head1 COPYRIGHT AND AUTHORS

See analizo(1)

=cut

use strict;

use Analizo::Command;
use Analizo::Extractor;
use Analizo::Metrics;
use Analizo::Extractor::Sloccount;

use Pod::Usage;
use Getopt::Long;
my $__list        = undef;
my $__extractor   = 'Doxyparse';
my $__global_only = undef;
my $__output      = undef;
my $__help        = undef;
my $__language    = undef;
my $__exclude     = undef;

GetOptions(
  'list|l'        => \$__list,
  'extractor=s' => \$__extractor,
  'global-only' => \$__global_only,
  'output=s'    => \$__output,
  'language=s'  => \$__language,
  'help'        => \$__help,
  'exclude|x=s' => \$__exclude,
) or exit(1);

if ($__help) {
  exec('analizo', 'doc', __FILE__)
}

if($__list){
    my $metrics_handler = new Analizo::Metrics(model => new Analizo::Model);
    my %metrics = $metrics_handler->list_of_metrics();
    my %global_metrics = $metrics_handler->list_of_global_metrics();

    print "Global Metrics:\n";
    foreach my $key (sort keys %global_metrics){
        print "$key - $global_metrics{$key}\n";
    }

    print "\nModule Metrics:\n";
    foreach my $key (sort keys %metrics){
        print "$key - $metrics{$key}\n";
    }
    exit(0);
}

my @unreadable = grep { ! -r $_ || ! -e $_ } @ARGV;
if (@unreadable) {
  foreach my $file (@unreadable) {
    printf STDERR "%s: %s is not readable\n", 'analizo metrics', $file;
  }
  exit(2);
}

if(!@ARGV){
    pod2usage(-verbose => 1, -exitval => 0)
}

my $extractor = Analizo::Extractor->load($__extractor);
my $eloc_extractor = Analizo::Extractor::Sloccount->new(model => $extractor->model);
if ($__language) {
  require Analizo::LanguageFilter;
  my $language_filter = Analizo::LanguageFilter->new($__language);
  $extractor->filters($language_filter);
  $eloc_extractor->filters($language_filter);
}
if ($__exclude) {
  my @excluded_directories = split(':', $__exclude);
  $extractor->exclude(@excluded_directories);
  $eloc_extractor->exclude(@excluded_directories);
}
my $metrics = Analizo::Metrics->new(model => $extractor->model);

if ($__global_only) {
  $metrics->report_global_metrics_only(1);
}


$extractor->process(@ARGV);
$eloc_extractor->process(@ARGV);

if ($__output) {
  open STDOUT, '>', $__output or die($!);
}

print $metrics->report;
close STDOUT;

exit(0);

