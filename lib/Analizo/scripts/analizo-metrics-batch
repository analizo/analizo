#!/usr/bin/perl

=head1 NAME

analizo-metrics-batch - processes several source code directories in batch,
running B<analizo metrics> for each and optionally consolidating the results in
a single data file.

=head1 USAGE

analizo metrics-batch [OPTIONS] [directory [directory ...]]

=head1 DESCRIPTION

B<analizo metrics-batch> is useful when you want to analyze several projects at
once, or several different versions of the same project. For example, suppose
you want to process 5 consecutive releases of I<myproject>, from version 0.1.0
to 0.5.0.

=over

=item

First you unpack the release tarballs for those versions in a directory, say
/tmp/analysis:

  $ ls -1 /tmp/analysis
  myproject-0.1.0
  myproject-0.2.0
  myproject-0.3.0
  myproject-0.4.0
  myproject-0.5.0

=item

Then you change to that directory, and then run B<analizo metrics-batch>:

  $ cd /tmp/analysis
  $ analizo metrics-batch

=item

B<analizo metrics-batch> will collect the metrics
data in a single .csv file, that you can import in spreadsheet software or
statistical packages.

=back

=head1 OPTIONS

=over

=item --help

Displays manual page (which you are currently reading).

=item -o FILE, --output FILE

Write output to FILE. Default is to write to I<metrics.csv>.  That file can
then be opened in data analysis programs.

=item -q, --quiet

Supresses messages to standard output.

=back

=head1 COPYRIGHT AND AUTHORS

See B<analizo(1)>.

=cut

use strict;
use warnings;

use Pod::Usage;
use Getopt::Long;

my $__help          = undef;
my $__output        = 'metrics.csv';
my $__quiet         = undef;
my $__parallel      = undef;

GetOptions(
  'help'            => \$__help,
  'o|output=s'      => \$__output,
  'q|quiet'         => \$__quiet,
  'p|parallel=i' => \$__parallel,
) or exit(1);

if ($__help) {
  exec('analizo', 'doc', __FILE__)
}

use Analizo::Batch::Directories;
use Analizo::Batch::Output::CSV;

my $runner = undef;
if ($__parallel) {
  require Analizo::Batch::Runner::Parallel;
  $runner = new Analizo::Batch::Runner::Parallel($__parallel);
} else {
  require Analizo::Batch::Runner::Sequential;
  $runner = new Analizo::Batch::Runner::Sequential;
}

unless ($__quiet) {
  $runner->before_each_job(
    sub {
      my $job = shift;
      printf("I: Processing %s ...\n", $job->id);
    }
  );
}

my $batch = new Analizo::Batch::Directories(@ARGV);

my $output = new Analizo::Batch::Output::CSV;
$output->file($__output);

$runner->run($batch, $output);
