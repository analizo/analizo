#!/usr/bin/perl -w

=head1 NAME

analizo-graph - dependency graph generator

=head1 USAGE

  analizo graph [options] <input>...

=head1 DESCRIPTION

analizo graph reads the dependency information from one or more source code
directories passed as arguments, and produces as output the graph of
dependencies between the modules of the software in the graphviz(1) format.

analizo graph is part of the analizo suite.

=head1 REQUIRED ARGUMENTS

=over

=item <input>...

The input directories (or files) with source code to be processed.

Although you can pass individual files as input, this tool is more useful if
you pass entire source directories. If you pass just a couple of files, their
dependencies on modules that are not declared and/or implemented in those
files will not be calculated.

=back

=head1 OPTIONS

=over

=item --omit <functions>, -t <functions>

Omit the given functions from the call graph.  Multiple function names
may be given separated by commas.

=item --cluster, -c

Cluster the functions into files, so you can see in which files are the calling
and called functions.

=item --modules, -m

Group the functions by modules(files), and only represent calls between
modules. This is useful to see the dependencies between the modules of the
program, intead of focusing on specific functions. The arrows between the
modules will be labelled with a number that represents the number of different
places in which the calling module calls functions in the called module (i.e.
how many times module A calls module B).

It doesn't make much sense to use --modules together with --cluster.

=item --extractor <extractor>, -e <extractor>

Define wich extractor method use to analise source code. Currenly "Doxyparse"
is the only extractor available.  Default is Doxyparse.

=item --output <file>, -o <file>

Use a file as output

=item --help, -h

Displays help on command line syntax and options.

=back

=head1 VIEWING THE GRAPH

To view the generated graph, pipe analizo's output to one of the
Graphiz tools. You can use B<dotty(1)> to display the graph in a
X11 window:

    $ analizo graph src/ | dotty -

You can also generate a file to print or include in a document
by using the B<dot(1)>.

To generate a PNG image called F<graph.png>:

  $ analizo graph src/ | dot -Tpng -ograph.png -

To generate a PostScript version of the dependency graph for printing, you can
also use the B<dot>. For example, to generate a dependency graph in the file
F<graph.ps> fitting everything on a US letter size page in landscape mode, try

  $ analizo graph src/ | dot -Grotate=90 -Gsize=11,8.5 -Tps -o graph.ps

Sometimes, the graph will fit better if the dependencies arrows go from left to
right instead of top to bottom.  The B<dot> option B<-Grankdir=LR> will do
that:

  $ analizo graph src/ | dot -Gsize=8.5,11 -Grankdir=LR -Tps -o graph.ps

For large software, the graph may end up too small
to comfortably read.  If that happens, try N-up printing:

  $ analizo graph src/ | dot -Gpage=8.5,11 -Tps -o graph.ps

You can also try playing with other B<dot> options such as B<-Gratio>,
or for a different style of graph, try using B<neato> instead of
B<dot>.  See the Graphwiz documentation for more information about the
various options available for customizing the style of the graph.

=head1 READING THE GRAPH

When generating a graph in the function level (i.g. without the I<--modules>
option), function calls are displayed as solid arrows.  A dotted arrow means
that the function the arrow points from takes the address of the function the
arrow points to; this typically indicates that the latter function is being
used as a callback.

When the I<--modules> option is on, then there are only solid arrows. An arrow
from I<A> to I<B> means that I<A> depends on I<B>.

=head1 SEE ALSO

B<dotty(1)>, B<dot(1)>, B<neato(1)>, B<analizo(1)>

=head1 COPYRIGHT AND AUTHORS

See B<analizo(1)>

=cut

use strict;

use Analizo::Command;
use Analizo::Extractor;
use Analizo::Output::DOT;

use Pod::Usage;
use Getopt::Long;

my $__extractor = 'Doxyparse';
my $__omit = '';
my $__cluster = undef;
my $__modules = undef;
my $__output = undef;
my $__help = undef;
GetOptions(
  'extractor|e=s' => \$__extractor,
  'omit|t=s'      => \$__omit,
  'cluster|c'     => \$__cluster,
  'modules|m'     => \$__modules,
  'output|o=s'    => \$__output,
  'help|h'        => \$__help,
) or exit(1);

if ($__help) {
  exec('analizo', 'doc', __FILE__)
}

if(!@ARGV){
  exec('analizo', 'doc', '--usage', __FILE__);
}

my $extractor = Analizo::Extractor->load($__extractor);
my $model = $extractor->model;
my $output = new Analizo::Output::DOT(model => $model);

# functions to omit
for my $omited (split(/,/, $__omit)) {
  $output->omit($omited);
}

# whether to cluster functions by file or not
$output->cluster($__cluster);

# wheter to group by module or not
$output->group_by_module($__modules);

my @unreadable = grep { ! -r $_ || ! -e $_ } @ARGV;
if (@unreadable) {
  foreach my $file (@unreadable) {
    printf STDERR "%s: %s is not readable\n", 'analizo metrics', $file;
  }
  exit(2);
}

$extractor->process(@ARGV);

if ($__output) {
  open STDOUT, '>', $__output or die($!);
}
print $output->string;
close STDOUT;

exit(0);
