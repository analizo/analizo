#!/usr/bin/perl -w

=head1 NAME

analizo-dsm - draw the design structure matrix from call graph

=head1 USAGE

  analizo dsm [options] <input>...

=head1 DESCRIPTION

analizo dsm reads the dependency information from one or more source code
directories passed as arguments, and produces as output the design structure
matrix from the dependencies between files of the software.

analizo dsm is part of the analizo suite.

=head1 OPTIONS

=over

=item --help

Displays help for B<analizo dsm>.

=item --extractor <extractor>

Define wich extractor method use to analise source code. Default is Doxyparse.

=item --output <file>, -o <file>

Writes output to <file>. Default is "dsm.png".

=item --exclude <dirs> | -x <dirs>

Exclude <I<dirs>> (a colon-separated list of directories) from the analysis.
This is useful, for example, when you want to focus on production code and
exclude test code from the analysis. You could do that by passing something
like pass B<--exclude test>.

=back

=cut

use strict;
use Analizo::Command;
use Analizo::Extractor;
use Graph::Writer::DSM;
use Pod::Usage;
use Getopt::Long;

my $__extractor = 'Doxyparse';
my $__output = 'dsm.png';
my $__exclude = undef;
my $__help = undef;
GetOptions(
  'extractor=s' => \$__extractor,
  'output=s'    => \$__output,
  'exclude|x=s' => \$__exclude,
  'help'        => \$__help,
) or exit(1);

if ($__help) {
  exec('analizo', 'doc', __FILE__)
}

if(!@ARGV){
  exec('analizo', 'doc', '--usage', __FILE__);
}

my $extractor = Analizo::Extractor->load($__extractor);

if ($__exclude) {
  my @excluded_directories = split(':', $__exclude);
  $extractor->exclude(@excluded_directories);
}

my $model = $extractor->model;

my @unreadable = grep { ! -r $_ || ! -e $_ } @ARGV;
if (@unreadable) {
  foreach my $file (@unreadable) {
    printf STDERR "%s: %s is not readable\n", 'analizo dsm', $file;
  }
  exit(2);
}

$extractor->process(@ARGV);
my $graph = $model->graph();

my $wr = Graph::Writer::DSM->new();
$wr->write_graph($graph, $__output);

exit 0;

=head1 COPYRIGHT AND AUTHORS

See B<analizo(1)>
