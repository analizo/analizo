#!/usr/bin/perl -w

=head1 NAME

analizo-dsm - draw the design structure matrix from call graph

=head1 USAGE

  analizo dsm [options] <input>...

=head1 DESCRIPTION

analizo dsm reads the dependency information from one or more source code
directories passed as arguments, and produces as output the design structure
matrix from the dependencies between files of the software.

analizo dsm is part of the analizo suite.

=head1 OPTIONS

=over

=item --help

Displays help for B<analizo dsm>.

=item --extractor <extractor>

Define wich extractor method use to analise source code. Default is Doxyparse.

=item --format <format>, -f <format>

Choice of output format to use. Supported formats are B<png> (the default; good
for very large code bases), and B<html> (good for small-medium code bases).

=item --output <file>, -o <file>

Writes output to <file>. Default is "dsm.<format>".

=item --exclude <dirs> | -x <dirs>

Exclude <I<dirs>> (a colon-separated list of directories) from the analysis.
This is useful, for example, when you want to focus on production code and
exclude test code from the analysis. You could do that by passing something
like pass B<--exclude test>.

=back

=cut

use strict;
use Analizo::Command;
use Analizo::Extractor;

use File::Basename;
use Cwd 'abs_path';
use Graph::Writer::DSM;
use Graph::Writer::DSM::HTML;
use Pod::Usage;
use Getopt::Long;

my $__extractor = 'Doxyparse';
my $__format = 'png';
my $__output = undef;
my $__exclude = undef;
my $__help = undef;
GetOptions(
  'extractor=s' => \$__extractor,
  'format=s'    => \$__format,
  'output=s'    => \$__output,
  'exclude|x=s' => \$__exclude,
  'help'        => \$__help,
) or exit(1);

if ($__help) {
  exec('analizo', 'doc', __FILE__)
}

if(!@ARGV){
  exec('analizo', 'doc', '--usage', __FILE__);
}

my $graph_writer;
if ($__format eq 'png') {
  $graph_writer = Graph::Writer::DSM->new();
} elsif ($__format eq 'html') {
  my $name = join(', ', map { basename(abs_path($_)) } @ARGV);
  $graph_writer = Graph::Writer::DSM::HTML->new(
    title => 'Design Structure matrix for ' . $name
  );
} else {
  printf STDERR "%s is not a valid output format.\n", $__format;
  exit(1);
}
$__output ||= 'dsm.' . $__format;

my $extractor = Analizo::Extractor->load($__extractor);

if ($__exclude) {
  my @excluded_directories = split(':', $__exclude);
  $extractor->exclude(@excluded_directories);
}

my $model = $extractor->model;

my @unreadable = grep { ! -r $_ || ! -e $_ } @ARGV;
if (@unreadable) {
  foreach my $file (@unreadable) {
    printf STDERR "%s: %s is not readable\n", 'analizo dsm', $file;
  }
  exit(2);
}

$extractor->process(@ARGV);
my $graph = $model->graph();

$graph_writer->write_graph($graph, $__output);

exit 0;

=head1 COPYRIGHT AND AUTHORS

See B<analizo(1)>
